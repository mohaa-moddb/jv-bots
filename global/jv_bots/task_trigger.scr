/**
Multiplayer Bot script for MoH:AA obj
=============================================
by jv_map
version 1.1 (supplemental)
---------------------------------------------
Copyright (c) 2002-2003 Jeroen Vrijkorte
All rights reserved.
---------------------------------------------
Distribution is allowed provided all subsequent 
conditions are met:

1. Commercial use is prohibited.

2. The above copyright notice and this permission notice shall
be included in all copies or substantial portions of the file.

3. If you have made modifications to the original files you must
cause the modified files to carry prominent notices stating
that you changed the files.

COPYRIGHT HOLDERS WILL NOT BE LIABLE FOR ANY DIRECT, INDIRECT, 
SPECIAL OR CONSEQUENTIAL DAMAGES ARISING OUT OF ANY USE OF THE 
SOFTWARE OR DOCUMENTATION.
---------------------------------------------
jv_map@planetmedalofhonor.com
http://www.planetmedal.com/freebrief
---------------------------------------------
*/

setup:
end

getpriority:
	local.bestnode = NULL
	local.bestscore = 0
	for(local.i = 1; local.i <= $triggernode.size; local.i++)
	{
		local.node = $triggernode[local.i]
		local.score = waitthread evaluate local.node
		if(local.score > local.bestscore)
		{
			local.bestnode = local.node
			local.bestscore = local.score
		}
	}
end (local.bestscore::local.bestnode)

evaluate local.parm local.onlyeval:
	local.node = local.parm[1]
	local.trigger = local.node.target

	if(local.trigger.weight != NIL)
		local.weight = local.trigger.weight
	else
		local.weight = 1

	local.score = 0

	if(local.trigger.disabled != 1)
	{
		if(local.trigger.triggerteam == self.dmteam)
		{
			local.distance = vector_length (self.origin - local.node.origin)
			local.score = 1024.0 / (local.distance + 1024) * local.weight
		}
	}
end local.score

action:
	// run to the trigger and activate it
	local.node = self.parm[1]
	local.trigger = local.node.target
	
	waitthread level.jvbot_main_script::action_go_to_routed local.node 32

	if(isAlive self && self.task == trigger && self.parm[1] == local.node)
	{
		// point of no return
		self.notask = 1
		self turnto local.trigger
		self lookat local.trigger
		waitthread turndone 1
		if(isAlive self)
		{
			local.trigger.other = self // can't set parm.other :(
			local.trigger.triggertime = level.time
			local.trigger activatetrigger self
			local.trigger triggerthread
			wait 0.25
			if(isAlive self)
			{
				self turnto NULL
				self lookat NULL
				self.notask = 0
				self.task = idle
			}
		}
	}
end

turndone local.timeout:
	thread turndonethread 
	local.thread = parm.previousthread
	local.time = 0
	local.fps = int(getcvar(sv_fps))
	while(local.time < local.timeout && local.thread)
	{
		waitframe
		local.time += (1.0 / local.fps)
	}
	if(local.thread)
		local.thread end
end

turndonethread:
	self waittill turndone
end