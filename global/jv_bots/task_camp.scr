/**
Multiplayer Bot script for MoH:AA obj
=============================================
by jv_map
version 1.1
---------------------------------------------
Copyright (c) 2002-2003 Jeroen Vrijkorte
All rights reserved.
---------------------------------------------
Distribution is allowed provided all subsequent 
conditions are met:

1. Commercial use is prohibited.

2. The above copyright notice and this permission notice shall
be included in all copies or substantial portions of the file.

3. If you have made modifications to the original files you must
cause the modified files to carry prominent notices stating
that you changed the files.

COPYRIGHT HOLDERS WILL NOT BE LIABLE FOR ANY DIRECT, INDIRECT, 
SPECIAL OR CONSEQUENTIAL DAMAGES ARISING OUT OF ANY USE OF THE 
SOFTWARE OR DOCUMENTATION.
---------------------------------------------
jv_map@planetmedalofhonor.com
http://www.planetmedal.com/freebrief
---------------------------------------------
*/

/**
---------------------------------------------
Camp Behaviour

Very similar to sniper behaviour.
---------------------------------------------
*/

setup:
	if($camper)
	{
		for(local.i = 1; local.i <= $camper.size; local.i++)
		{
			local.node = $camper[local.i]
			if(local.weight == NIL)
				local.node.weight = 1
		}
		waitthread level.jvbot_main_script::addroute camper 1
	}
end

/**
Priority factors:
	-weapon
	-distance
	-node #weigth
	-node #noallies / #noaxis keys
This task cannot be performed in-combat.
*/
getpriority:
	local.bestscore = 0
	local.bestnode = NULL
	local.weight = 0
	if(self.enemy == NULL)
	{
		if($camper.size > 0)
		{
			switch(self.weapon)
			{
				case "shotgun":
					local.weight = 4
					break
				case "thompson":
				case "mp40":
				case "stg44":
					local.weight = 2
					break
				case "bar":
				case "m1 garand":
					local.weight = 1
					break
				default:
					local.weight = -1
			}
			if(local.weight > 0)
			{
				for(local.i = 1; local.i <= $camper.size; local.i++)
				{
					local.node = $camper[local.i]
					local.score = waitthread evaluate (local.node::local.weight)
					if(local.score > local.bestscore)
					{
						local.bestnode = local.node
						local.bestscore = local.score	
					}
				}
			}
		}
	}
	local.output = local.bestscore::local.bestnode::local.weight
end local.output

evaluate local.parm local.onlyeval:
	local.node = local.parm[1]
	local.weaponweight = local.parm[2]
	local.score = 0
	if !((local.node.noallies == 1 && self.dmteam == allies) || (local.node.noaxis == 1 && self.dmteam == axis))
	{
		if(local.node.camperenroute != 1 || local.node.camper == self)
		{
			local.distance = vector_length (self.origin - local.node.origin)
			if(local.distance < 256)
				// prevent over reacting
				local.distance = 256
			local.score = 0.1 * (1024 / (local.distance + 1024)) + local.node.weight * local.weaponweight
		}
	}
end local.score

action:
	local.location = self.parm[1]
	if(local.location.camperenroute != 1)
	{
		local.location.camperenroute = 1
		local.location.camper = self
		self thread level.jvbot_modscript[debug]::debugmodifylight 64 blue
		self thread level.jvbot_modscript[debug]::debugmodifylight 64 red
		self waitthread level.jvbot_main_script::action_go_to_routed local.location 64
		if(isAlive self && self.task == camp)
		{
			local.location.hascamper = 1
			if(local.location.target != "")
				self aimat local.location.target

			local.leash = self.leash
			local.mindist = self.mindist
			self.mindist = 0
			self.leash = 0
			self resetleash
			self.fixedleash = 1
			local.health = self.health
			while(isAlive self && self.task == camp && self.parm[1] == local.location)
			{
				if(self.health < local.health)
				{
					self.notask = 1
					self.mindist = 128
					self.leash = 512
					self aimat self.fact.attacker
					wait 1
					while(isAlive self && isAlive self.enemy)
						wait 0.5
					self.task = idle // restart task
					self.notask = 0
				}
				waitframe
				wait level.jvbot_loopdelay
			}
			if(isAlive self)
			{
				self aimat NULL
				self.fixedleash = 0
				self.leash = local.leash
				self.mindist = local.mindist
			}
			local.location.hascamper = 0
		}
		if(isAlive self)
		{
			self thread level.jvbot_modscript[debug]::debugmodifylight -64 blue
			self thread level.jvbot_modscript[debug]::debugmodifylight -64 red
		}
		local.location.camperenroute = 0
		local.location.camper = NULL
	}
	else
		self.task = "idle"
end