/**
Bomb script for MoH:AA obj
=============================================
by jv_map
version 1.1
---------------------------------------------
Copyright (c) 2002-2003 Jeroen Vrijkorte
All rights reserved.
---------------------------------------------
Distribution is allowed provided all subsequent 
conditions are met:

1. Commercial use is prohibited.

2. The above copyright notice and this permission notice shall
be included in all copies or substantial portions of the file.

3. If you have made modifications to the original files you must
cause the modified files to carry prominent notices stating
that you changed the files.

COPYRIGHT HOLDERS WILL NOT BE LIABLE FOR ANY DIRECT, INDIRECT, 
SPECIAL OR CONSEQUENTIAL DAMAGES ARISING OUT OF ANY USE OF THE 
SOFTWARE OR DOCUMENTATION.
---------------------------------------------
jv_map@planetmedalofhonor.com
http://www.planetmedal.com/freebrief
---------------------------------------------
*/

/**
---------------------------------------------
Advanced Bomb Thinker
---------------------------------------------
*/

/**
Setup thread. Run immediately after level spawn.

The following keys can be set per bomb:
#defuse_time:		time to defuse a bomb in tenths of a second.
#set_time:			time to plant the bomb in tenths of a second.
#tick_time:			time before the bomb explodes in seconds.
$plantteam:			team that can plant a bomb here, 'allies' or 'axis'.
$explosivemodel:	filename to model TIKI if you want to use a custom bomb model.
$trigger_name:		targetname of trigger_use connected to the bomb.
*/
bomb_thinker:
	if(self.defuse_time == NIL)
		self.defuse_time = 60 //tenths of a second
	if(self.set_time == NIL)
		self.set_time = 50  //tenths of a second
	if(self.tick_time == NIL)
		self.tick_time = 45
		
	self.explosion_radius = 1054  //quake units
	self.usedistance = 128 //quake units
	self.damage = 200
	self.usefov = 30
	self.exploded = 0
	
	if(self.plantteam == NIL)
	{
		if(level.planting_team == NIL)
			self.plantteam = "allies"
		else
			self.plantteam = level.planting_team
	}
	
	if(level.team_bombs_planted[self.plantteam] == NIL)
		level.team_bombs_planted[self.plantteam] = 0
		
	if(level.team_targets_destroyed[self.plantteam] == NIL)
		level.team_targets_destroyed[self.plantteam] = 0
		
	if(level.bomb_set == NIL)
		level.bomb_set = 0
		
	if(self.plantteam == allies)
		self.defuseteam = axis
	else
		self.defuseteam = allies
	
	if(self.target != "")
	{
		self.target.collisionent = self.target.target
		self.target notsolid
	}
	
	if(self.model == NIL)
		self.pulsemodel = "items/pulse_explosive.tik"
	else
		self.pulsemodel = self.model
	
	if(self.explosivemodel == NIL)
		self.explosivemodel = "items/explosive.tik"
	
	cache self.explosivemodel
	if (self.explosion_fx != NIL)
		cache self.explosion_fx

	self model self.pulsemodel
	self notsolid
	self.trigger = $(self.trigger_name)
	
	thread roundstart
	
	//* Roundstart may be faked by setting level.roundstart = 1
	while (level.roundstart != 1)
		wait 0.5
	
	self thread bomb_trigger
end

/**
Sets level.roundstart to 1 when the round starts.
*/
roundstart:
	level waittill roundstart
	level.roundstart = 1
end

/**
Bomb plant trigger thread. Internal use.
*/
bomb_trigger:
	if(self.trigger != NIL && self.trigger != NULL)
	{
		while(self.exloded != 1)
		{
			self.trigger waittill trigger
			self waitthread bomb_set parm.other
		}
		self.trigger remove
	}
	else
		println "WARNING[jv_obj_dm::bomb_trigger]: Bomb " self " doesn't have a trigger."
end

/**
Bomb defuse trigger thread. Internal use.
*/
bomb_defuse_trigger:
	if(self.trigger != NIL && self.trigger != NULL)
	{
		while(self != NIL && self.ticking == 1)
		{
			self.trigger waittill trigger
			self waitthread bomb_defuse parm.other
		}
	}
	else
		println "WARNING[jv_obj_dm::bomb_defuse_trigger]: Bomb " self " doesn't have a trigger."
end

/**
Signals this bomb to being set by the given planter.
*/
bomb_set local.planter:
	if(level.roundstart != 1 || self.exploded == 1 || self.beingset == 1 || self.beingdefused == 1 || self.ticking == 1 || !(isAlive local.planter) || local.planter.dmteam != self.plantteam)
		end
	
	local.planter threatbias 100
	
	local.playerplanter = 0
	if(local.planter.classname == "Player")
		local.playerplanter = 1
	
	println "INFO[jv_obj_dm::bomb_set]: Bomb being planted by " local.planter.targetname
	
	self.beingset = 1
	
	if(local.playerplanter == 1)
	{
		local.planter stopwatch (self.set_time / 10)
		local.fov = self.usefov
	}
	else
	{
		local.planter.useheld = 1
		local.fov = 360
	}
		
	local.time = 0
	
	while(local.time <= self.set_time && isAlive local.planter && local.planter cansee self local.fov self.usedistance && local.planter.useheld == 1)
	{
		wait 0.1
		local.time += 1
	}
	if(isAlive local.planter)
	{
		if(local.playerplanter == 1)
			local.planter stopwatch 0
		
		if(local.planter.ingame != 0)
			local.planter threatbias 0
	}
	self.beingset = 0
	if(local.time >= self.set_time)
	{
		self.ticking = 1
		self waitthread bomb_planted
	}
end

/**
Defines this bomb as 'planted' and runs timer and explosion threads.
*/
bomb_planted:
	self.ticking = 1 // just to be sure
	// set regular game variable
	level.bombs_planted++
	level.bomb_set++
	// set custom variable
	level.team_bombs_planted[self.plantteam]++
	self model self.explosivemodel
	self solid
	iprintlnbold "A Bomb has been planted!"
	
	if (self.plantteam == "allies")
		self playsound dfr_objective_o
	else
		self playsound den_objective_o

	self thread bomb_tick
end

/**
Sets the given bomb as being defused.
*/
bomb_defuse local.antiplanter:
	if(self.exploded == 1 || self.beingset == 1 || self.beingdefused == 1 || self.ticking != 1 || !(isAlive local.antiplanter) || local.antiplanter.dmteam != self.defuseteam)
		end
	
	self.beingdefused = 1
	local.antiplanter threatbias 100
	
	local.playerplanter = 0
	if(local.antiplanter.classname == "Player")
		local.playerplanter = 1
	
	println "INFO[jv_obj_dm::bomb_defuse]: Bomb being defused by " local.antiplanter.targetname
	
	if(local.playerplanter == 1)
	{
		local.antiplanter stopwatch (self.defuse_time / 10)
		local.fov = self.usefov
	}
	else
	{
		local.antiplanter.useheld = 1
		local.fov = 360
	}
		
	local.time = 0
	
	while(local.time <= self.defuse_time && isAlive local.antiplanter && local.antiplanter cansee self local.fov self.usedistance && local.antiplanter.useheld == 1 && self.exploded != 1)
	{
		wait 0.1
		local.time += 1
	}
	if(local.anitplanter != NIL && isAlive local.antiplanter)
	{
		if(local.playerplanter == 1)
			local.antiplanter stopwatch 0
		
		if(local.anitplanter.ingame != 0)
			local.antiplanter threatbias 0
	}
	self.beingdefused = 0
	if(local.time >= self.defuse_time)
	{
		self.defused = 1
		self waitthread bomb_defused
	}
end

/**
Sets the bomb to have been defused.
*/
bomb_defused:
	if(self.exploded == 1)
		end
	
	// set regular game variables
	level.bombs_planted--
	level.bomb_set--
	// set custom variable
	level.team_bombs_planted[self.plantteam]--
	self.beingdefused = 0
	self.defused = 1
	self model self.pulsemodel
	self notsolid
	iprintlnbold "A Bomb has been defused!"
	if (self.plantteam == "axis")
		self playsound dfr_diffused_d
	else
		self playsound den_diffused_d
end

/**
Bomb tick thread. Internal use.
*/
bomb_tick:
	self.ticking = 1
	self thread bomb_defuse_trigger
	self loopsound bombtick
	local.time = 0
	// first countdown
	while(self.defused != 1 && local.time < (self.tick_time - 10))
	{
		wait 0.1
		local.time += .1
	}
	self stoploopsound
	if(self.defused != 1)
	{
		self.critical = 1
		// final countdown
		self loopsound final_countdown
		while(self.defused != 1 && local.time < self.tick_time)
		{
			wait 0.1
			local.time += 0.1
		}
		self stoploopsound
		if(self.defused != 1)
		{
			// boom!
			self waitthread bomb_explode		
		}
	}
	self.critical = 0
	self.ticking = 0
	self.defused = 0
end

/**
Explodes the bomb.
*/
bomb_explode:
	level.bombs_planted--
	level.targets_destroyed++
	level.team_bombs_planted[self.plantteam]--
	level.team_targets_destroyed[self.plantteam]++
	// don't decrease level.bomb_set
	// this would cause malfunctioning if there are multiple bombs
			
	self.exploded = 1
	self.trigger nottriggerable
	
	if (self.exploder_set != NIL)
		exec global/exploder.scr::explode self.exploder_set
	
	if (self.explosion_fx != NIL)
		self thread spawn_fx self.explosion_fx
	
	if (self.explosion_sound != NIL)
		self playsound self.explosion_sound

	if (self.target != "")
	{
		if (self.target.destroyed_model != NIL)
			local.damaged = self.target thread spawn_damaged self.target.destroyed_model
		self.target remove
	}
	radiusdamage (self.origin + (0 0 32)) self.damage self.explosion_radius
	if (self.killarea != NIL)
		self.killarea volumedamage 1000
	self hide
	
	earthquake .35 10 0 0
	earthquake .23 6 0 0
	earthquake 1 1 0 0
	earthquake 1.25 .3 0 1
end

/**
Spawns the given special effect.
*/
spawn_fx local.fx:
	local.temp = spawn script_model model local.fx
	local.temp.origin = self.origin
	local.temp anim start
	wait 5
	local.temp remove
end

/**
Spawns the given model with 'idle' animation.
*/
spawn_damaged local.model:
	local.damaged = spawn script_model model local.model
	local.damaged.origin = self.origin
	local.damaged.angles = self.angles
end local.damaged